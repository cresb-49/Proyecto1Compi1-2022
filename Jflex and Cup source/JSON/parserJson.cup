package com.cresb49.appcliente.analizadores.json;

import java.util.ArrayList;

import com.cresb49.appcliente.analizadores.ErrorAnalisis;
import com.cresb49.appcliente.analizadores.Token;
import com.cresb49.appcliente.ED.Pila;
import com.cresb49.appcliente.analizadores.json.obj.*;


import java_cup.runtime.*;

class ParserJson;

parser code {:
    // Connect this parser to a scanner!

    private static final String ERROR_TYPE_SIN = "Sintactico";
    private static final String ERROR_TYPE_SEM = "Semantico";

    private LexerJson lexerJson;
    private ArrayList<ErrorAnalisis> errorAnalisisesTmp;
    private SimbolosTerminalesJson simbolosTerminalesJson;
    private ReporteJson reporteJson;

    
    public ParserJson (LexerJson lexerJson){ 
        super(lexerJson);
        this.lexerJson=lexerJson;
        this.simbolosTerminalesJson = new SimbolosTerminalesJson();
    }

    public ReporteJson getReporteJson() {
        return reporteJson;
    }

    public void report_error(String message, Object info) {
        System.out.println("public void report_error");
    }
    
    public void report_fatal_error(String message, Object info) {
        System.out.println("public void report_fatal_error");
    }

    public void syntax_error(Symbol cur_token) {
        Token tok = (Token) cur_token.value;
        if(tok!=null){
            if (cur_token.sym == ParserJsonSym.EOF) {
                String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminalesJson.obtenerSimbolos(expected_token_ids()).toString();
                System.out.println("FIN ARCHIVO"+" "+er);
                this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,"FIN ARCHIVO", cur_token.left, cur_token.right, er));
                System.out.println(er);
            } else {
                String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminalesJson.obtenerSimbolos(expected_token_ids()).toString();
                System.out.println(tok.getLexema()+" "+er);
                this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,tok.getLexema(), tok.getLinea(), tok.getColumna(), er));
                System.out.println(er);
            }
        }else{
            String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminalesJson.obtenerSimbolos(expected_token_ids()).toString();
            System.out.println("FIN ARCHIVO"+" "+er);
            this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,"FIN ARCHIVO", cur_token.left, cur_token.right, er));
            System.out.println(er);
        }
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
        if (cur_token.sym == ParserJsonSym.EOF) {
            String er = "Error irrecuperable se llego al final del archivo";
            System.out.println("FIN ARCHIVO"+" "+er);
            this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,"FIN ARCHIVO", cur_token.left, cur_token.right, er));
            System.out.println(er);
        } else {
            Token tok = (Token) cur_token.value;
            //String er = "Error irrecuperable, un posible simbolo esperado: "+ simbolosTerminalesJson.obtenerSimbolos(expected_token_ids()).toString();
            String er = "Error irrecuperable resuelva el error anterior";
            System.out.println(tok.getLexema()+" "+er);
            this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN, tok.getLexema(), tok.getLinea(), tok.getColumna(), er));
            System.out.println(er);
        }
    }

    private void semantic_error(Token token,String contexto) {
        this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,token.getLexema(), token.getLinea(), token.getColumna(), contexto));
    }

    private boolean definition_error(Token ini,Token fin,ArrayList<String> errores){
        boolean status = false;
        for (String errore : errores) {
            status = true;
            String error = "Error en definicion, "+errore+" Ubicacion general --> Linea: "+ini.getLinea()+" a Linea: "+fin.getLinea();
            this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM, ini.getLexema(), ini.getLinea(), ini.getColumna(), error));
            System.out.println(error);
        }
        return status;
    }

    private void verificar_reporte_json(ReporteJson verReporteJson,Token ref){
        if(verReporteJson.getScore() == null){
          String error = "Error en definicion, el resultado Json no tiene Score";
          this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,"Score", ref.getLinea(),ref.getColumna(), error));
        }
        if(verReporteJson.getClases() == null){
          String error = "Error en definicion, el resultado Json no tiene lista de Clases";
          this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,"Clases", ref.getLinea(),ref.getColumna(), error));
        }
        if(verReporteJson.getVariables() == null){
          String error = "Error en definicion, el resultado Json no tiene lista de Variables";
          this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,"Variables", ref.getLinea(),ref.getColumna(), error));
        }
        if(verReporteJson.getMetodos() == null){
          String error = "Error en definicion, el resultado Json no tiene lista de Metodos";
          this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,"Metodos", ref.getLinea(),ref.getColumna(), error));
        }
        if(verReporteJson.getComentarios() == null){
          String error = "Error en definicion, el resultado Json no tiene lista de Comentarios";
          this.lexerJson.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,"Comentarios", ref.getLinea(),ref.getColumna(), error));
        }
    }

    private Metodo verificar_metodo(Token valname, Token valtipo, Token valpara) {
        if(valname.getLexema().isEmpty()||valname.getLexema().isBlank()){
            semantic_error(valname,"El valor de la cadena de texto esta vacia");
        }
        if(valtipo.getLexema().isEmpty()||valtipo.getLexema().isBlank()){
            semantic_error(valtipo,"El valor de la cadena de texto esta vacia");
        }
        return new Metodo(valname.getLexema(), valtipo.getLexema(), Integer.valueOf(valpara.getLexema()));
    }

    private Variable verificar_variable(Token valname, Token valtipo, Token valFun) {
        if(valname.getLexema().isEmpty()||valname.getLexema().isBlank()){
            semantic_error(valname,"El valor de la cadena de texto esta vacia");
        }
        if(valtipo.getLexema().isEmpty()||valtipo.getLexema().isBlank()){
            semantic_error(valtipo,"El valor de la cadena de texto esta vacia");
        }
        if(valFun.getLexema().isEmpty()||valFun.getLexema().isBlank()){
            semantic_error(valFun,"El valor de la cadena de texto esta vacia");
        }
        return new Variable(valname.getLexema(), valtipo.getLexema(), valFun.getLexema());
    }

    protected int error_sync_size() {
		return 1;
	}
:}

/* define how to connect to the scanner! */
scan with {: return this.lexerJson.next_token(); :};

/* Terminals (tokens returned by the scanner). */
terminal Token  CO_A,CO_C,LLA_A,LLA_C,COMA,DOSPUNTOS,STRING,ENTERO,
                SCORE,CLASS,VAR,NAME,TYPE,FUN,METD,COMENT,PARA,TEXT;


/* Non terminals */
non terminal                        ini;
non terminal ReporteJson            reporte,reportep;
non terminal ArrayList<Clase>       contClass,contClassp;
non terminal ArrayList<Variable>    contVar,contVarp;
non terminal ArrayList<Metodo>      contMet,contMetp;
non terminal ArrayList<Comentario>  contCom,contComp;
non terminal Variable               expVar;
non terminal Metodo                 expMet;


/* The grammar rules */

ini ::= LLA_A:ref reportep:r LLA_C  {:
                                        if(r!=null){
                                            reporteJson=r;
                                            verificar_reporte_json(reporteJson,ref);
                                        }
                                    :}
    ;
/*Gramatica para reporte Json*/
reportep    ::= reporte:r SCORE:p DOSPUNTOS STRING:str  {:
                                                            RESULT=r;
                                                            if(RESULT!=null){
                                                                Token tokenTmp = str;
                                                                boolean bandera = true;
                                                                if(tokenTmp.getLexema().isEmpty()||tokenTmp.getLexema().isBlank()){
                                                                    semantic_error(tokenTmp,"El valor de la cadena de texto esta vacia");
                                                                    bandera=false;
                                                                }
                                                                String val="";
                                                                try {
                                                                    Double val2 = Double.valueOf(tokenTmp.getLexema());
                                                                    val = val2.toString();
                                                                } catch (NumberFormatException e) {
                                                                    bandera=false;
                                                                    semantic_error(tokenTmp,"El valor de la cadena no representa un valor numerico");
                                                                }
                                                                if(RESULT.getScore()!=null){
                                                                    semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                    bandera=false;
                                                                }
                                                                if(bandera){
                                                                    RESULT.setScore(val);
                                                                }
                                                            }
                                                        :}
            |   reporte:r CLASS:p DOSPUNTOS CO_A contClass:cc CO_C  {:
                                                                        RESULT=r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getClases()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setClases(cc);
                                                                            }
                                                                        }
                                                                    :}
            |   reporte:r CLASS:p DOSPUNTOS CO_A CO_C  {:
                                                                        RESULT=r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getClases()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setClases(new ArrayList<>());
                                                                            }
                                                                        }
                                                                    :}
            |   reporte:r VAR:p DOSPUNTOS CO_A contVar:cv CO_C  {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getVariables()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setVariables(cv);
                                                                        }
                                                                    }
                                                                :}
            |   reporte:r VAR:p DOSPUNTOS CO_A CO_C  {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getVariables()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setVariables(new ArrayList<>());
                                                                        }
                                                                    }
                                                                :}
            |   reporte:r METD:p DOSPUNTOS CO_A contMet:cm CO_C {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getMetodos()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setMetodos(cm);
                                                                        }
                                                                    }
                                                                :}
            |   reporte:r METD:p DOSPUNTOS CO_A CO_C            {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getMetodos()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setMetodos(new ArrayList<>());
                                                                        }
                                                                    }
                                                                :}
            |   reporte:r COMENT:p DOSPUNTOS CO_A contCom:cme CO_C  {:
                                                                        RESULT = r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getComentarios()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setComentarios(cme);
                                                                            }
                                                                        }
                                                                    :}
            |   reporte:r COMENT:p DOSPUNTOS CO_A CO_C  {:
                                                                        RESULT = r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getComentarios()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setComentarios(new ArrayList<>());
                                                                            }
                                                                        }
                                                                    :}
            |   reporte:r error                         {:RESULT = r;:}
            ;

reporte ::= reporte:r SCORE:p DOSPUNTOS STRING:str COMA   {:
                                                            RESULT=r;
                                                            if(RESULT!=null){
                                                                Token tokenTmp = str;
                                                                boolean bandera = true;
                                                                if(tokenTmp.getLexema().isEmpty()||tokenTmp.getLexema().isBlank()){
                                                                    semantic_error(tokenTmp,"El valor de la cadena de texto esta vacia");
                                                                    bandera=false;
                                                                }
                                                                String val="";
                                                                try {
                                                                    Double val2 = Double.valueOf(tokenTmp.getLexema());
                                                                    val = val2.toString();
                                                                } catch (NumberFormatException e) {
                                                                    bandera=false;
                                                                    semantic_error(tokenTmp,"El valor de la cadena no representa un valor numerico");
                                                                }
                                                                if(RESULT.getScore()!=null){
                                                                    semantic_error(p,"La propiedad ya a sido generada con anterioridad");
                                                                    bandera=false;
                                                                }
                                                                if(bandera){
                                                                    RESULT.setScore(val);
                                                                }
                                                            }
                                                        :}
        |   reporte:r CLASS:p DOSPUNTOS CO_A contClass:cc CO_C COMA   {:
                                                                        RESULT=r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getClases()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setClases(cc);
                                                                            }
                                                                        }
                                                                    :}
        |   reporte:r CLASS:p DOSPUNTOS CO_A CO_C COMA   {:
                                                                        RESULT=r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getClases()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setClases(new ArrayList<>());
                                                                            }
                                                                        }
                                                                    :}
        |   reporte:r VAR:p DOSPUNTOS CO_A contVar:cv CO_C COMA   {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getVariables()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setVariables(cv);
                                                                        }
                                                                    }
                                                                :}
        |   reporte:r VAR:p DOSPUNTOS CO_A CO_C COMA            {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getVariables()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setVariables(new ArrayList<>());
                                                                        }
                                                                    }
                                                                :}
        |   reporte:r METD:p DOSPUNTOS CO_A contMet:cm CO_C COMA  {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getMetodos()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setMetodos(cm);
                                                                        }
                                                                    }
                                                                :}
        |   reporte:r METD:p DOSPUNTOS CO_A CO_C COMA  {:
                                                                    RESULT=r;
                                                                    if(RESULT!=null){
                                                                        if(RESULT.getMetodos()!=null){
                                                                            semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                        }else{
                                                                            RESULT.setMetodos(new ArrayList<>());
                                                                        }
                                                                    }
                                                                :}
        |   reporte:r COMENT:p DOSPUNTOS CO_A contCom:cme CO_C COMA   {:
                                                                        RESULT = r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getComentarios()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setComentarios(cme);
                                                                            }
                                                                        }
                                                                    :}
        |   reporte:r COMENT:p DOSPUNTOS CO_A CO_C COMA             {:
                                                                        RESULT = r;
                                                                        if(RESULT!=null){
                                                                            if(RESULT.getComentarios()!=null){
                                                                                semantic_error(p,"La propiedad ya habia sido definida con anterioridad");
                                                                            }else{
                                                                                RESULT.setComentarios(new ArrayList<>());
                                                                            }
                                                                        }
                                                                    :}
        |                                               {:
                                                            RESULT = new ReporteJson();
                                                        :}
        |   reporte:r error                             {:RESULT = r;:}
        ;

contClass   ::= contClassp:cc LLA_A NAME DOSPUNTOS STRING:str LLA_C         {:
                                                                                RESULT = cc;
                                                                                if(RESULT!=null){
                                                                                    Token token = str;
                                                                                    if(token.getLexema().isEmpty()||token.getLexema().isBlank()){
                                                                                        semantic_error(token,"El valor de la cadena de texto esta vacia");
                                                                                    }else{
                                                                                        Clase clase = new Clase(token.getLexema());
                                                                                        RESULT.add(clase);
                                                                                    }
                                                                                }
                                                                            :}
            |   contClassp:cc error     {:RESULT = cc;:}
            ;

contClassp  ::= contClassp:cc LLA_A NAME DOSPUNTOS STRING:str LLA_C COMA    {:
                                                                                RESULT = cc;
                                                                                if(RESULT!=null){
                                                                                    Token token = str;
                                                                                    if(token.getLexema().isEmpty()||token.getLexema().isBlank()){
                                                                                        semantic_error(token,"El valor de la cadena de texto esta vacia");
                                                                                    }else{
                                                                                        Clase clase = new Clase(token.getLexema());
                                                                                        RESULT.add(clase);
                                                                                    }
                                                                                }
                                                                            :}
            |   {:RESULT = new ArrayList<>();:}
            |   contClassp:cc error     {:RESULT = cc;:}
            ;

contVar ::= contVarp:cv LLA_A expVar:expv LLA_C                 {:
                                                                    RESULT = cv;
                                                                    if(RESULT!=null){
                                                                        RESULT.add(expv);
                                                                    }
                                                                :}
        |   contVarp:cv error   {:RESULT = cv;:}
        ;

contVarp    ::= contVarp:cv LLA_A expVar:expv LLA_C COMA    {:
                                                                    RESULT = cv;
                                                                    if(RESULT!=null){
                                                                        RESULT.add(expv);
                                                                    }
                                                                :}
            |   {:RESULT = new ArrayList<>();:}
            |   contVarp:cv error {:RESULT = cv;:}
            ;

contMet ::= contMetp:cm LLA_A expMet:expm LLA_C             {:
                                                                RESULT = cm;
                                                                if(RESULT!=null){
                                                                    RESULT.add(expm);
                                                                }
                                                            :}
        |   contMetp:cm error   {:RESULT=cm;:}
        ;

contMetp    ::= contMetp:cm LLA_A expMet:expm LLA_C COMA    {:
                                                                RESULT = cm;
                                                                if(RESULT!=null){
                                                                    RESULT.add(expm);
                                                                }
                                                            :}
            |   {:
                    RESULT = new ArrayList<>();
                :}
            |   contMetp:cm error   {:RESULT=cm;:}
            ;

expVar  ::= NAME DOSPUNTOS STRING:valname COMA TYPE DOSPUNTOS STRING:valtipo COMA FUN DOSPUNTOS STRING:valFun   {:RESULT=verificar_variable(valname,valtipo,valFun);:}
        |   NAME DOSPUNTOS STRING:valname COMA FUN DOSPUNTOS STRING:valFun COMA TYPE DOSPUNTOS STRING:valtipo   {:RESULT=verificar_variable(valname,valtipo,valFun);:}
        |   FUN DOSPUNTOS STRING:valFun COMA NAME DOSPUNTOS STRING:valname COMA TYPE DOSPUNTOS STRING:valtipo   {:RESULT=verificar_variable(valname,valtipo,valFun);:}
        |   FUN DOSPUNTOS STRING:valFun COMA TYPE DOSPUNTOS STRING:valtipo COMA NAME DOSPUNTOS STRING:valname   {:RESULT=verificar_variable(valname,valtipo,valFun);:}
        |   TYPE DOSPUNTOS STRING:valtipo COMA FUN DOSPUNTOS STRING:valFun COMA NAME DOSPUNTOS STRING:valname   {:RESULT=verificar_variable(valname,valtipo,valFun);:}
        |   TYPE DOSPUNTOS STRING:valtipo COMA NAME DOSPUNTOS STRING:valname COMA FUN DOSPUNTOS STRING:valFun   {:RESULT=verificar_variable(valname,valtipo,valFun);:}
        ;

expMet  ::= NAME DOSPUNTOS STRING:valname COMA TYPE DOSPUNTOS STRING:valtipo COMA PARA DOSPUNTOS ENTERO:valpara  {:RESULT = verificar_metodo(valname,valtipo,valpara);:}
        |   NAME DOSPUNTOS STRING:valname COMA PARA DOSPUNTOS ENTERO:valpara COMA TYPE DOSPUNTOS STRING:valtipo  {:RESULT = verificar_metodo(valname,valtipo,valpara);:}
        |   TYPE DOSPUNTOS STRING:valtipo COMA PARA DOSPUNTOS ENTERO:valpara COMA NAME DOSPUNTOS STRING:valname  {:RESULT = verificar_metodo(valname,valtipo,valpara);:}
        |   TYPE DOSPUNTOS STRING:valtipo COMA NAME DOSPUNTOS STRING:valname COMA PARA DOSPUNTOS ENTERO:valpara  {:RESULT = verificar_metodo(valname,valtipo,valpara);:}
        |   PARA DOSPUNTOS ENTERO:valpara COMA TYPE DOSPUNTOS STRING:valtipo COMA NAME DOSPUNTOS STRING:valname  {:RESULT = verificar_metodo(valname,valtipo,valpara);:}
        |   PARA DOSPUNTOS ENTERO:valpara COMA NAME DOSPUNTOS STRING:valname COMA TYPE DOSPUNTOS STRING:valtipo  {:RESULT = verificar_metodo(valname,valtipo,valpara);:}
        ;

contCom ::= contComp:cme LLA_A TEXT DOSPUNTOS STRING:str LLA_C          {:
                                                                            RESULT = cme;
                                                                            if(RESULT!=null){
                                                                                Token token = str;
                                                                                Comentario comentario = new Comentario(token.getLexema());
                                                                                RESULT.add(comentario);
                                                                            }
                                                                        :}
        |   contComp:cme error  {:RESULT = cme;:}
        ;
contComp    ::= contComp:cme LLA_A TEXT DOSPUNTOS STRING:str LLA_C COMA {:
                                                                            RESULT = cme;
                                                                            if(RESULT!=null){
                                                                                Token token = str;
                                                                                Comentario comentario = new Comentario(token.getLexema());
                                                                                RESULT.add(comentario);
                                                                            }
                                                                        :}
            |   {:
                    RESULT = new ArrayList<>();
                :}
            |   contComp:cme error  {:RESULT = cme;:}
            ;

