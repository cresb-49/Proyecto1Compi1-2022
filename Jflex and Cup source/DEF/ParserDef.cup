package com.cresb49.appcliente.analizadores.def;

import java.util.ArrayList;

import com.cresb49.appcliente.analizadores.ErrorAnalisis;
import com.cresb49.appcliente.analizadores.json.obj.ReporteJson;
import com.cresb49.appcliente.analizadores.Token;
import com.cresb49.appcliente.ED.Pila;
import com.cresb49.appcliente.analizadores.def.obj.*;


import java_cup.runtime.*;

class ParserDef;

parser code {:
    // Connect this parser to a scanner!

    private static final String ERROR_TYPE_SIN = "Sintactico";
    private static final String ERROR_TYPE_SEM = "Semantico";

    private LexerDef lexerDef;
    private ArrayList<ErrorAnalisis> errorAnalisisesTmp;
    private SimbolosTerminalesDef simbolosTerminalesDef;
    private TablaSimbolos tablaSimbolos;
    private ReporteJson reporteJson;
    
    public ParserDef (LexerDef lexerDef){ 
        super(lexerDef);
        this.lexerDef=lexerDef;
        this.tablaSimbolos = new TablaSimbolos();
        this.simbolosTerminalesDef = new SimbolosTerminalesDef();
    }

    public void report_error(String message, Object info) {
        System.out.println("public void report_error");
    }
    
    public void report_fatal_error(String message, Object info) {
        System.out.println("public void report_fatal_error");
    }

    public void syntax_error(Symbol cur_token) {
        Token tok = (Token) cur_token.value;
        if(tok!=null){
            if (cur_token.sym == ParserDefSym.EOF) {
                String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminalesDef.obtenerSimbolos(expected_token_ids()).toString();
                //System.out.println("FIN ARCHIVO"+" "+er);
                this.lexerDef.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,"FIN ARCHIVO", cur_token.left, cur_token.right, er));
                //System.out.println(er);
            } else {
                String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminalesDef.obtenerSimbolos(expected_token_ids()).toString();
                //System.out.println(tok.getLexema()+" "+er);
                this.lexerDef.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,tok.getLexema(), tok.getLinea(), tok.getColumna(), er));
                //System.out.println(er);
            }
        }else{
            String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminalesDef.obtenerSimbolos(expected_token_ids()).toString();
            //System.out.println("FIN ARCHIVO"+" "+er);
            this.lexerDef.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,"FIN ARCHIVO", cur_token.left, cur_token.right, er));
            //System.out.println(er);
        }
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
        if (cur_token.sym == ParserDefSym.EOF) {
            String er = "Error irrecuperable se llego al final del archivo";
            //System.out.println("FIN ARCHIVO"+" "+er);
            this.lexerDef.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN,"FIN ARCHIVO", cur_token.left, cur_token.right, er));
            //System.out.println(er);
        } else {
            Token tok = (Token) cur_token.value;
            //String er = "Error irrecuperable, un posible simbolo esperado: "+ simbolosTerminalesDef.obtenerSimbolos(expected_token_ids()).toString();
            String er = "Error irrecuperable resuelva el error anterior";
            //System.out.println(tok.getLexema()+" "+er);
            this.lexerDef.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SIN, tok.getLexema(), tok.getLinea(), tok.getColumna(), er));
            //System.out.println(er);
        }
    }


    private void verificar_def_var(Token token,String tipo) {
       FilaTabla fila = tablaSimbolos.buscar(token.getLexema());
        if(fila==null){
            tablaSimbolos.getFilas().add(new FilaTabla(token.getLexema(),tipo,null));
        }else{
            semantic_error(token,"La variable ya a sido definida con anterioridad");
        }
    }

    private void verificar_tipo_var_for(Token identificador,String tipo){
        FilaTabla fila = tablaSimbolos.buscar(identificador.getLexema());
        if(fila!=null){
            if(!(fila.getTipo().equals(tipo))){
                semantic_error(identificador,"Debe utilizar una variable de tipo Integer");
            }
        }else{
            semantic_error(identificador,"La variable que sea utilizar no existe en el programa");
        }
    }
    private void varificar_no_null(Token identificador){
        FilaTabla fila = tablaSimbolos.buscar(identificador.getLexema());
        if(fila!=null){
            if(fila.getValor()==null){
                semantic_error(identificador,"La variable no esta inicializada");
            }
        }
    }

    private void asig_val_var(Token identificador,String tipo,Object value) {
       FilaTabla fila = tablaSimbolos.buscar(identificador.getLexema());
        if(fila==null){
            semantic_error(identificador,"La variable a la que le quiere asignar valor no esta definida");
        }else{
            if(fila.getTipo().equals(tipo)){
              fila.setValor(value);
            }else{
                semantic_error(identificador,"La variable es de tipo: \""+fila.getTipo()+"\", no puede asignar un \""+tipo+"\"");
            }
        }
    }

    private void create_var_asig_val(Token identificador,String tipo_id,String tipo_value,Object value) {
       FilaTabla fila = tablaSimbolos.buscar(identificador.getLexema());
        if(fila==null){
            if(tipo_id.equals(tipo_value)){
                tablaSimbolos.getFilas().add(new FilaTabla(identificador.getLexema(),tipo_id,value));
            }else{
                semantic_error(identificador,"La variable es de tipo: \""+tipo_id+"\", no puede asignar un \""+tipo_value+"\"");
            }
        }else{
            semantic_error(identificador,"La variable ya a sido definida con anterioridad");
            //semantic_error(identificador,"El valor que se desea dar a la variable de de tipo \"\" y la variable es de tipo \"\"");
        }
    }

    private void error_cast(Token operador,String tipo1,String tipo2){
        semantic_error(operador,"No se puede operar un valor: \""+tipo1+"\" con un valor:\""+tipo2+"\"");
    }

    private void semantic_error(Token token,String contexto) {
        this.lexerDef.getErrors().add(new ErrorAnalisis(ERROR_TYPE_SEM,token.getLexema(), token.getLinea(), token.getColumna(), contexto));
    }

    public TablaSimbolos getTablaSimbolos() {
        return tablaSimbolos;
    }

    protected int error_sync_size() {
		return 1;
	}
:}

/* define how to connect to the scanner! */
scan with {: return this.lexerDef.next_token(); :};

/* Terminals (tokens returned by the scanner). */
terminal        CO_A,CO_C,LLA_A,LLA_C,PA_A,PA_C,COMA,DOSPUNTOS,PUNTOCOMA,STRING,ENTERO,
                INT,STR,ITERATOR,HASTA,D_PA_A,PA_C_D,ID,EQUAL,ME_Q,MA_Q,ME_QB,MAS,
                MENOS,DIV,MUL;

terminal Token  HTML,H1,H2,TABLE,FOR,TR,TH,TD,BR,TEXT;


/* Non terminals */
non terminal                        ini,asig,dec,decn,def;
non terminal Pila<Token>            decp;
non terminal Operacion              exp,t,f;

non terminal                        html,htmlp,contHTML,inertags,inertagsp,inertagspp,table,tablec,trc,metFor,contFor,tdc,thc,tablecpp,actionfor,
                                    tdcp,thcp,paramhasta;


/* Inicio de las reglas gramticales*/

def ::= def html
    |   ini
    ;

ini ::= ini asig
    |   ini dec
    |   ini decn
    |   asig
    |   dec
    |   decn
    ;

/*Gramatica para declaracion de variables*/


decn   ::=  INT ID:var EQUAL exp:val PUNTOCOMA  {:
                                                    Token identificador = (Token)var;
                                                    if(val!=null){
                                                        create_var_asig_val((Token)var,TablaSimbolos.INT,val.getTipo(),val.getValor());
                                                    }else{
                                                        verificar_def_var(identificador,TablaSimbolos.INT);
                                                    }
                                                :}
        |   STR ID:var EQUAL exp:val PUNTOCOMA  {:
                                                    Token identificador = (Token)var;
                                                    if(val!=null){
                                                        create_var_asig_val((Token)var,TablaSimbolos.STRING,val.getTipo(),val.getValor());
                                                    }else{
                                                        verificar_def_var(identificador,TablaSimbolos.STRING);
                                                    }
                                                :}
        ;

dec    ::=  INT decp:vars   {:
                                if(vars!=null){
                                    ArrayList<Token> array =  vars.toArrayList();
                                    if(array!=null){
                                        for (Token token : array) {
                                            verificar_def_var(token, TablaSimbolos.INT);
                                        }
                                    }
                                }
                            :}
        |   STR decp:vars   {:
                                if(vars!=null){
                                    ArrayList<Token> array =  vars.toArrayList();
                                    if(array!=null){
                                        for (Token token : array) {
                                            verificar_def_var(token, TablaSimbolos.STRING);
                                        }
                                    }
                                }
                            :}
        ;
    
decp    ::= ID:id COMA decp:vars {:
                                    RESULT = vars;
                                    if(RESULT!=null){
                                        RESULT.push((Token)id);
                                    }else{
                                        RESULT = new Pila<>();
                                    }
                                :}
        |   ID:id PUNTOCOMA {:
                                RESULT = new Pila<>();
                                RESULT.push((Token)id);
                            :}
        ;

asig    ::= ID:var EQUAL exp:val PUNTOCOMA  {:
                                                Token identificador = (Token)var;
                                                if(val!=null){
                                                    asig_val_var(identificador,val.getTipo(),val.getValor());
                                                }
                                            :}
        ;


exp ::= exp:val1 MAS:s t:val2   {:  
                                    try {
                                        String tipo = Operacion.autoCast(val1.getTipo(),TablaSimbolos.MAS,val2.getTipo());
                                        Object result = Operacion.sumaTerminos(val1, val2);
                                        RESULT = new Operacion(tipo,result);
                                    } catch (NotCastException e) {
                                        error_cast((Token)s,val1.getTipo(),val2.getTipo());
                                        //System.out.println("No se pudo castear el resultado");
                                        RESULT = val1;
                                    }
                                :}
    |   exp:val1 MENOS:s t:val2 {:
                                    try {
                                        String tipo = Operacion.autoCast(val1.getTipo(),TablaSimbolos.MENOS,val2.getTipo());
                                        Integer result = ((Integer)val1.getValor())-((Integer)val2.getValor());
                                        RESULT = new Operacion(tipo,result);
                                    } catch (NotCastException e) {
                                        error_cast((Token)s,val1.getTipo(),val2.getTipo());
                                        //System.out.println("No se pudo castear el resultado");
                                        RESULT = val1;
                                    }
                                :}
    |   t:val   {:
                    RESULT = val;
                :}
    ;

t   ::= t:val1 MUL:s f:val2 {:
                                try {
                                    String tipo = Operacion.autoCast(val1.getTipo(),TablaSimbolos.MUL,val2.getTipo());
                                    Integer result = ((Integer)val1.getValor())*((Integer)val2.getValor());
                                    RESULT = new Operacion(tipo,result);
                                } catch (NotCastException e) {
                                    error_cast((Token)s,val1.getTipo(),val2.getTipo());
                                    //System.out.println("No se pudo castear el resultado");
                                    RESULT = val1;
                                }
                            :}
    |   t:val1 DIV:s f:val2 {:
                                try {
                                    String tipo = Operacion.autoCast(val1.getTipo(),TablaSimbolos.DIV,val2.getTipo());
                                    Integer result = ((Integer)val1.getValor())/((Integer)val2.getValor());
                                    RESULT = new Operacion(tipo,result);
                                } catch (NotCastException e) {
                                    error_cast((Token)s,val1.getTipo(),val2.getTipo());
                                    //System.out.println("No se pudo castear el resultado");
                                    RESULT = val1;
                                }
                            :}
    |   f:val   {:
                    RESULT = val;
                :}
    ;

f   ::= ID:val      {:
                        Token token = (Token) val;
                        FilaTabla simbolo = tablaSimbolos.buscar(token.getLexema());
                        if(simbolo!=null){
                            if(simbolo.getValor()!=null){
                                RESULT = new Operacion(simbolo.getTipo(),simbolo.getValor());
                            }else{
                                semantic_error(token, "La variable no esta inicializada");
                            }
                        }else{
                            semantic_error(token, "La variable no esta definida");
                        }
                    :}
    |   ENTERO:val  {:
                        Token token = (Token) val;
                        RESULT = new Operacion(TablaSimbolos.INT,Integer.parseInt(token.getLexema()));
                    :}
    |   STRING:val  {:
                        Token token = (Token) val;
                        RESULT = new Operacion(TablaSimbolos.STRING,token.getLexema());
                    :}
    |   PA_A exp:val PA_C   {:
                                RESULT = val;
                            :}
    ;
/*Gramatica para lenguaje HTML*/

html    ::= htmlp ME_QB HTML:tagfin MA_Q    {:
                                                tagfin.setValorToken("</hmtl>");
                                            :}
        ;

htmlp   ::= ME_Q HTML:tagini MA_Q contHTML  {:
                                                tagini.setValorToken("<hmtl>");
                                            :}
        ;

contHTML    ::= contHTML inertags
            |   contHTML table
            |   contHTML metFor
            |
            ;

inertags   ::=  ME_Q inertagspp
            ;

inertagspp  ::= BR:tag MA_Q {:
                                tag.setValorToken("<br>");
                            :}
            |   H1:tagini MA_Q inertagsp ME_QB H1:tagfin MA_Q   {:
                                                                    tagini.setValorToken("<h1>");
                                                                    tagfin.setValorToken("</h1>");
                                                                :}
            |   H2:tagini MA_Q inertagsp ME_QB H2:tagfin MA_Q   {:
                                                                    tagini.setValorToken("<h2>");
                                                                    tagfin.setValorToken("</h2>");
                                                                :}
            ;

inertagsp   ::= inertags
            |   TEXT:txt    {:
                                txt.setValorToken(txt.getLexema());
                            :}
            |   D_PA_A ID:var PA_C_D
            |
            ;

table   ::= ME_Q TABLE:tagini MA_Q tablec ME_QB TABLE:tagfin MA_Q   {:
                                                                        tagini.setValorToken("<table border=\"1\">");
                                                                        tagfin.setValorToken("</table>");
                                                                    :}
        |   error MA_Q
        ;

tablec  ::= tablec ME_Q tablecpp
        |   ME_Q inertagspp
        |   ME_Q TR:tagini MA_Q trc ME_QB TR:tagfin MA_Q    {:
                                                                tagini.setValorToken("<tr>");
                                                                tagfin.setValorToken("</tr>");
                                                            :}
        |   ME_Q actionfor
        |   tablec error
        ;

tablecpp    ::= TR:tagini MA_Q trc ME_QB TR:tagfin MA_Q {:
                                                            tagini.setValorToken("<tr>");
                                                            tagfin.setValorToken("</tr>");
                                                        :}
            |   inertagspp
            |   actionfor
            ;

trc ::= trc ME_Q TH:tagini MA_Q thc ME_QB TH:tagfin MA_Q    {:
                                                                tagini.setValorToken("<th>");
                                                                tagfin.setValorToken("</th>");
                                                            :}
    |   trc ME_Q TD:tagini MA_Q tdc ME_QB TD:tagfin MA_Q    {:
                                                                tagini.setValorToken("<td>");
                                                                tagfin.setValorToken("</td>");
                                                            :}
    |   trc inertags
    |   ME_Q TH:tagini MA_Q thc ME_QB TH:tagfin MA_Q        {:
                                                                tagini.setValorToken("<th>");
                                                                tagfin.setValorToken("</th>");
                                                            :}
    |   ME_Q TD:tagini MA_Q tdc ME_QB TD:tagfin MA_Q        {:
                                                                tagini.setValorToken("<td>");
                                                                tagfin.setValorToken("</td>");
                                                            :}
    |   inertags
    |   trc error
    ;

thc ::= thcp
    |
    ;

thcp ::= thcp inertags
    |   thcp TEXT:txt                {:
                                        txt.setValorToken(txt.getLexema());
                                    :}
    |   thcp D_PA_A ID:var PA_C_D    {:

                                    :}
    |   inertags
    |   TEXT:txt                    {:
                                        txt.setValorToken(txt.getLexema());
                                    :}
    |   D_PA_A ID:var PA_C_D        {:
                                        
                                    :}
    |   thcp error
    ;

tdc ::= tdcp
    |
    ;

tdcp ::= tdcp inertags   
    |   tdcp TEXT:txt                {:
                                        txt.setValorToken(txt.getLexema());
                                    :}
    |   tdcp D_PA_A ID:var PA_C_D    {:
                                        
                                    :}
    |   inertags
    |   TEXT:txt                    {:
                                        txt.setValorToken(txt.getLexema());
                                    :}
    |   D_PA_A ID:var PA_C_D        {:
                                        
                                    :}
    |   tdcp error
    ;

metFor  ::= ME_Q actionfor
        ;

actionfor   ::= FOR:tagini ITERATOR:it DOSPUNTOS ID:var_ite HASTA:has DOSPUNTOS paramhasta:max PUNTOCOMA MA_Q contFor:contenido ME_QB FOR:tagfin MA_Q   {:
                                                                                                                                                            verificar_tipo_var_for((Token)var_ite,TablaSimbolos.INT);
                                                                                                                                                            varificar_no_null((Token)var_ite);
                                                                                                                                                            tagini.setAccion(Token.BUCLE_INI);
                                                                                                                                                            tagfin.setAccion(Token.BUCLE_FIN);
                                                                                                                                                        :}
            ;
paramhasta ::=  ID:var  {:
                            verificar_tipo_var_for((Token)var,TablaSimbolos.INT);
                            varificar_no_null((Token)var);
                            
                        :}
            |   ENTERO:en   {:
                                RESULT = ((Token)en).getValue();
                            :}
            |   error MA_Q
            ;

contFor ::= tablec
        ;